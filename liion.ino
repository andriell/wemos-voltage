// Напряжение полностью заряженного 18650 4.2В
// Его нельзя разряжать ниже 2,4В
// A0 имеет АЦП. Пик 3,3В соответствует значению 1024 при analogRead(A0);
// A0 притянут к земле через резистор на 320 К этот резистор можно использовать как половину делителя напряжения.
// Если A0 подключить через резистор на 100К к плюсу 18650 то делитель напряжения будет завершенным.
// Соответствие напряжений на аккумуляторе и значений analogRead(A0) измеренных экспериментально
// 2.4V - 595
// 2.6V - 645
// 2.8V - 694
// 3.0V - 746
// 3.2V - 796
// 3.4V - 847
// 3.6V - 897
// 3.8V - 944
// 4.0V - 997
// 4.12V - 1024 (Вычислено)
// Видно что зависимость получилась линейной. Империческая формала для расчета напряжения analogRead(A0) / 248.5
// Минимальное напряжение при котором WeMos D1 mini еще работает это 2.6 вольта.

void liionSetup() {
  pinMode(WEMOS_A0, INPUT);
}

unsigned int liionRaw() {
  unsigned int raw = analogRead(A0);
  dbg(2, "liion raw = ");
  dbgLn(2, raw);
  return raw;
}

// Напржение аккумулятора
float liionV() {
  unsigned int raw = analogRead(A0);
  float v = float(raw) / 248.5f;
  dbg(2, "liion raw: ");
  dbg(2, raw);
  dbg(2, "; V: ");
  dbgLn(2, v);
  return v;
}

// Заряд аккумулятора в процентах
// Полный разряд 2.6 вольта
int liionP() {
  unsigned int raw = analogRead(A0);
  int p = round((float(raw) - 645.0f) / 3.79f);
  if (p < 0) {
    p = 0;
  }
  if (p > 100) {
    p = 100;
  }
  dbg(2, "liion raw: ");
  dbg(2, raw);
  dbg(2, "; P: ");
  dbgLn(2, p);
  return p;
}
